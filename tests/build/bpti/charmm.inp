* setup bpti

! read predefined topologies(rtf) and forcefields(par)
read rtf   card name ../../param/top_all27_prot_lipid.rtf
read param card name ../../param/par_all27_prot_lipid.prm

! read sequence from pdb
open unit 21 read form name pdb/4pti_prot.pdb
read sequence pdb unit 21
close unit 21

! link residues according to the topologies(rtf)
generate prot setup warn first NTER last CTER

! make disulfide bonds
patch disu prot  5 prot 55
autogenerate angles dihedrals
patch disu prot 14 prot 38
autogenerate angles dihedrals
patch disu prot 30 prot 51
autogenerate angles dihedrals

! read coordinates from pdb
read coor pdb name pdb/4pti_prot.pdb

! check missing coords of heavy atoms and build them
print coor select .not. hydrogen .and. .not. initialized end
ic param 
ic build
print coor select .not. hydrogen .and. .not. initialized end

! build hydrogens and check missing coords of all atoms
hbuild select hydrogen end
print coor select .not. initialized end

! check whether we can calculate energy
energy

! read xtal waters
!read sequence tip3 167
!gene xwat noangle nodihedral

! rename atom name and residue type to read pdb
!rename atom O sele atom xwat * OH2 end
!rename resn HOH sele segid xwat end

! read coordinates from pdb 
!read coor pdb name pdb/4pti_xwat.pdb offset -1

! rename back
!rename atom OH2 sele atom xwat * O end
!rename resn TIP3 sele segid xwat end

! build hydrogens
!hbuild sele hydrogen end

! check whether we can calculate energy
!energy

! orient protein and set XMAX, XMIN
coor stat sele segi prot end 
coor orie sele segi prot end 
coor stat sele segi prot end

! define box size with 8A padding
calc XSIZ = INT( ?XMAX - ?XMIN + 16)
calc YSIZ = INT( ?YMAX - ?YMIN + 16)
calc ZSIZ = INT( ?ZMAX - ?ZMIN + 16)

! solvate by water box
define SOLUTE sele all end
stream solvent-box.str

! check whether we can calculate energy
energy

! add ions
define WATREPL sele segid wat .and. -
(resi 1 .or. resi 373 .or. resi 801 .or. resi 1301 .or. resi 1999 .or. resi 2501 ) end
set ion cla
coor stat sele WATREPL .and. .not. hydrogen end 
set nions ?nsel
read sequence @ion @nions
gene ions 
coor dupl sele WATREPL .and. .not. hydrogen end sele segid ions end
delete atom sele .byres. watrepl end
join wat renumber

! setup Periodic Boundary Conditions
! Here we use the image facility, but crystal works as well and 
! for a simple minimum image setup there is also the pbound method.
! Variables XSIZ,YSIZ, and ZSIZ have already been defined
open unit 10 read form name ortho.img
read image card unit 10
! also specify that water molecules should be recentered as molecules (residues) if they 
! get out of primary box, but that the protein molecule is a whole segment
image byresidue sele resn tip3 end 
image bysegment sele segi prot end
image bysegment sele segi ions end

! apply SHAKE on all bonds X-H
shake bonh param

! Steepest Descent
minimize sd nstep 300 cdie eps 1.0 fshift vshift cutnb 13.0 ctofnb 12.0

! check whether we can calculate energy
energy

write title name CELLSIZE
* (X, Y, Z) = (@XSIZ, @YSIZ, @ZSIZ)
*

write psf card name bpti.psf
* bpti solvated in box @XSIZ x @YSIZ x @ZSIZ
*

write psf card xplor name bpti_xplor.psf
* bpti solvated in box @XSIZ x @YSIZ x @ZSIZ
*

write coor pdb name bpti.pdb
* bpti solvated in box @XSIZ x @YSIZ x @ZSIZ
*

write coor card name bpti.crd
* bpti solvated in box @XSIZ x @YSIZ x @ZSIZ
*

