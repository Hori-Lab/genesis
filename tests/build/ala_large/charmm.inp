* setup alanine-dipeptide

! read predefined topologies(rtf) and forcefields(par)
read rtf   card name ../../param/top_all27_prot_lipid.rtf
read param card name ../../param/par_all27_prot_lipid.prm

! read sequence from pdb
open unit 21 read form name pdb/ala_original.pdb
read sequence pdb unit 21
close unit 21

! link residues according to the topologies(rtf)
generate prot setup warn 

! read coordinates from pdb
read coor pdb name pdb/ala_original.pdb

autogenerate angles dihedrals

! check missing coords of all atoms
print coor select .not. initialized end

! check whether we can calculate energy
energy

! orient protein and set XMAX, XMIN
coor stat sele segi prot end 
coor orie sele segi prot end 
coor stat sele segi prot end

! define box size with 18A padding
calc XSIZ = INT( ?XMAX - ?XMIN + 56)
calc YSIZ = INT( ?YMAX - ?YMIN + 61)
calc ZSIZ = INT( ?ZMAX - ?ZMIN + 62)

! solvate by water box
define SOLUTE sele all end
stream solvent-box.str

! check whether we can calculate energy
energy

! setup Periodic Boundary Conditions
! Here we use the image facility, but crystal works as well and 
! for a simple minimum image setup there is also the pbound method.
! Variables XSIZ,YSIZ, and ZSIZ have already been defined
open unit 10 read form name ortho.img
read image card unit 10
! also specify that water molecules should be recentered as molecules (residues) if they 
! get out of primary box, but that the protein molecule is a whole segment
image byresidue sele resn tip3 end 
image bysegment sele segi prot end

! apply SHAKE on all bonds X-H
shake bonh param

! Steepest Descent
minimize sd nstep 300 cdie eps 1.0 fshift vshift cutnb 13.0 ctofnb 12.0

! check whether we can calculate energy
energy

write title name CELLSIZE
* (X, Y, Z) = (@XSIZ, @YSIZ, @ZSIZ)
*

write psf card name ala.psf
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*

write psf card xplor name ala_xplor.psf
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*

write coor pdb name ala.pdb
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*

write coor card name ala.crd
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*


