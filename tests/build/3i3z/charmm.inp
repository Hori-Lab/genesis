* Generate PSF and CHARMM coordinate
*

! --- Read in Topology and  Parameter files

open unit 1 card read name /home/mdsoft/charmm/c35b2/toppar/top_all27_prot_lipid.rtf
read RTF card unit 1
close unit 1

open unit 1 card read name /home/mdsoft/charmm/c35b2/toppar/par_all27_prot_lipid.prm
read PARA card unit 1
close unit 1


! --- Read sequence from the PDB coordinate file

! Read PROA
open read card unit 10 name ./pdb/proa.pdb
read sequence pdb unit 10
generate PROA setup warn first GLYP last CTER

open read card unit 10 name ./pdb/proa.pdb
read coor pdb  unit 10 resid

! Read PROB
open read card unit 10 name ./pdb/prob.pdb
read sequence pdb unit 10
generate PROB setup warn first NTER last CTER

open read card unit 10 name ./pdb/prob.pdb
read coor pdb  unit 10 resid


! Disulfide bonds
patch disu PROA 6 PROA 11 setup warn
autogenerate angles dihedrals

patch disu PROA 7 PROB 7 setup warn
autogenerate angles dihedrals

patch disu PROA 20 PROB 19 setup warn
autogenerate angles dihedrals


! Water
READ SEQUENCE TIP3 2326
GENERATE SOLV SETUP NOANGLE NODIHEDRAL

open read card unit 10 name ./pdb/solv.pdb
read coor pdb  unit 10 resid


! Ions
READ SEQUENCE SOD 8
GENERATE SOD WARN

open read card unit 10 name ./pdb/sod.pdb
read coor pdb  unit 10 resid

READ SEQUENCE CLA 6
GENERATE CLA WARN

open read card unit 10 name ./pdb/cla.pdb
read coor pdb  unit 10 resid


! --- write psf, pdb, and crd file

open write formatted unit 1 name 3i3z.psf
write psf card unit 1
close unit 1

open unit 1 card write name 3i3z.pdb
write coor pdb unit 1
close unit 1

open unit 1 card write name 3i3z.crd
write coor unit 1 card
close unit 1


stop
