* setup alanine-dipeptide

! read predefined topologies(rtf) and forcefields(par)
read rtf   card name ../../param/top_all27_prot_lipid.rtf
read param card name ../../param/par_all27_prot_lipid.prm

! read sequence from pdb
open unit 21 read form name pdb/ala_original.pdb
read sequence pdb unit 21
close unit 21

! link residues according to the topologies(rtf)
generate prot setup warn 

! read coordinates from pdb
read coor pdb name pdb/ala_original.pdb

autogenerate angles dihedrals

! check missing coords of all atoms
print coor select .not. initialized end

! check whether we can calculate energy
energy

! orient protein and set XMAX, XMIN
coor stat sele segi prot end 
coor orie sele segi prot end 
coor stat sele segi prot end


write psf card name ala_only.psf
* alanine-dipeptide, no water 
*

write psf card xplor name ala_only_xplor.psf
* alanine-dipeptide, no water 
*

write coor pdb name ala_only.pdb
* alanine-dipeptide, no water 
*

write coor card name ala_only.crd
* alanine-dipeptide, no water 
*


! define box size with 18A padding
calc XSIZ = INT( ?XMAX - ?XMIN + 36)
calc YSIZ = INT( ?YMAX - ?YMIN + 36)
calc ZSIZ = INT( ?ZMAX - ?ZMIN + 36)

! solvate by water box
define SOLUTE sele all end
stream solvent-box.str

! check whether we can calculate energy
energy

write title name CELLSIZE
* (X, Y, Z) = (@XSIZ, @YSIZ, @ZSIZ)
*

write psf card name ala.psf
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*

write psf card xplor name ala_xplor.psf
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*

write coor pdb name ala.pdb
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*

write coor card name ala.crd
* alanine-dipeptide and waters in box @XSIZ x @YSIZ x @ZSIZ
*


