* setup bpti

! read predefined topologies(rtf) and forcefields(par)
read rtf   card name ../../param/top_all27_prot_lipid.rtf
read param card name ../../param/par_all27_prot_lipid.prm

! define box size with 8A padding
calc XSIZ = 64
calc YSIZ = 64
calc ZSIZ = 64

! solvate by water box
define SOLUTE sele all end
stream solvent-box.str

! check whether we can calculate energy
energy

! setup Periodic Boundary Conditions
! Here we use the image facility, but crystal works as well and 
! for a simple minimum image setup there is also the pbound method.
! Variables XSIZ,YSIZ, and ZSIZ have already been defined
open unit 10 read form name ortho.img
read image card unit 10
! also specify that water molecules should be recentered as molecules (residues) if they 
! get out of primary box, but that the protein molecule is a whole segment
image byresidue sele resn tip3 end 

! apply SHAKE on all bonds X-H
shake bonh param

! Steepest Descent
minimize sd nstep 300 cdie eps 1.0 fshift vshift cutnb 13.0 ctofnb 12.0

! check whether we can calculate energy
energy

write title name CELLSIZE
* (X, Y, Z) = (@XSIZ, @YSIZ, @ZSIZ)
*

write psf card name water.psf
* water in box @XSIZ x @YSIZ x @ZSIZ
*

write psf card xplor name water_xplor.psf
* water in box @XSIZ x @YSIZ x @ZSIZ
*

write coor pdb name water.pdb
* water in box @XSIZ x @YSIZ x @ZSIZ
*

write coor card name water.crd
* water in box @XSIZ x @YSIZ x @ZSIZ
*

